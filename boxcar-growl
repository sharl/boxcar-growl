#!/usr/bin/env perl

use strict;
use warnings;
use File::Basename;
use Config::Tiny;
use LWP::UserAgent;
use Getopt::Long;

my $prog = basename($0);
my $conf = $ENV{HOME}.'/.'.$prog;
(my $source = $prog) =~ s/[_-]/ /g;
my $r = GetOptions(
    'sound=s' => \my $sound,
);
if (! $r or $#ARGV < 0) {
    print <<EOF;
usage: $prog [-s|--sound <sound name>] <title> [message]

	-s|--sound <sound>	specify sound name

sound name list:

   https://boxcar.uservoice.com/knowledgebase/articles/306788-how-to-send-your-boxcar-account-a-notification

EOF
;
    exit -1;
}

my $title = shift @ARGV;
my $message = join(' ', @ARGV);
if (! $message) {
    my @message = <STDIN>;
    $message = join('', @message);
}
my $ini = Config::Tiny->read($conf) || die Config::Tiny->errstr;

my %formdata = (
    user_credentials => $ini->{_}{token},
    'notification[title]' => $title,
    'notification[long_message]' => $message,
    'notification[sound]' => $sound || 'clanging',
    'notification[source_name]' => $source,
    );

my $res = LWP::UserAgent->new(timeout => 10)->post(
    'https://new.boxcar.io/api/notifications',
    \%formdata);
if (! $res->is_success) {
    print $res->message . "\n";
}
